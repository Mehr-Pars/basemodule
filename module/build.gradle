apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def libraryGroupId = project.libraryGroup
def libraryArtifactId = project.libraryName
def libraryVersion = project.versionName

android {
    signingConfigs {
        release {
            storeFile file(project.mpars_sign_key_dir)
            storePassword project.mpars_sign_key_password
            keyAlias = project.mpars_sign_key_alias
            keyPassword project.mpars_sign_key_password
        }
    }

    compileSdkVersion project.compileSdkVersion.toInteger()
    defaultConfig {
        minSdkVersion project.minSdkVersion.toInteger()
        targetSdkVersion project.targetSdkVersion.toInteger()
        versionCode project.versionCode.toInteger()
        versionName project.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        android.buildFeatures.dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testEspresso"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    api "androidx.core:core-ktx:$coreVersion"
    api "com.google.android.material:material:1.7.0"
    api "com.android.support:multidex:$multidexVersion"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    api "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // room
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    // navigation component
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Rx
    api "io.reactivex.rxjava3:rxandroid:$rxAndroidVersion"
    api "io.reactivex.rxjava3:rxjava:$rxJavaVersion"

    // Network
    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    api "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    api "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    api "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:$retrofitAdapterVersion" // RxJava3 Adapter for retrofit

    // reactive network check
    implementation "com.github.pwittchen:reactivenetwork-rx2:$reactiveNetworkVersion"

    //imageLoader
    api "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    //dataBinding
    kapt "com.android.databinding:compiler:3.2.0-alpha10"

    // countly log service
    implementation "ly.count.android:sdk:$countlyVersion"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/module-release.aar")

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                // for dependencies and exclusions
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dp.group)
                    dependencyNode.appendNode('artifactId', dp.name)
                    dependencyNode.appendNode('version', dp.version)

                    // for exclusions
                    if (dp.excludeRules.size() > 0) {
                        def exclusions = dependencyNode.appendNode('exclusions')
                        dp.excludeRules.each { ExcludeRule ex ->
                            def exclusion = exclusions.appendNode('exclusion')
                            exclusion.appendNode('groupId', ex.group)
                            exclusion.appendNode('artifactId', ex.module)
                        }
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://maven2.mpars.ir/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = mpars_artifactory_username
            password = mpars_artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

task generateSources {
    doFirst {
        def script = "kotlinc _generator_helper.kt -include-runtime -d _generator.jar".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()
    }
    doLast {
        def script = "java -jar _generator.jar".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()

        "rm _generator.jar".execute()
    }
}

task assembleModule {
    doFirst {
        def script = "./gradlew assemble".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()
    }
}

task publishModule {
    doFirst {
        def script = "./gradlew artifactoryPublish".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()
    }
}

assembleModule.dependsOn generateSources
publishModule.dependsOn assembleModule