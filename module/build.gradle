apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'maven'

apply plugin: "androidx.navigation.safeargs.kotlin"

def libraryGroupId = project.libraryGroup
def libraryArtifactId = project.libraryName
def libraryVersion = project.versionName

android {
    signingConfigs {
        release {
            storeFile file(project.mpars_sign_key_dir)
            storePassword project.mpars_sign_key_password
            keyAlias = project.mpars_sign_key_alias
            keyPassword project.mpars_sign_key_password
        }
    }

    compileSdkVersion project.compileSdkVersion.toInteger()
    defaultConfig {
        minSdkVersion project.minSdkVersion.toInteger()
        targetSdkVersion project.targetSdkVersion.toInteger()
        versionCode project.versionCode.toInteger()
        versionName project.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
        vectorDrawables.useSupportLibrary = true
        android.dataBinding.enabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testEspresso"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation "com.android.support:multidex:$multidexVersion"
    api "androidx.core:core-ktx:$coreVersion"
    api "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    api "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshVersion"
    api "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    api "androidx.appcompat:appcompat:$supportVersion"
    api "com.google.android.material:material:$supportVersion"

    // lifecycle components
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"

    //dataBinding
    kapt "com.android.databinding:compiler:$dataBindingSupport"

    // paging library
    api "androidx.paging:paging-runtime-ktx:$pagingVersion"
//    implementation "androidx.paging:paging-rxjava2-ktx:$pagingVersion"

    // Kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // room
    kapt "androidx.room:room-compiler:$roomVersion"
//    api "androidx.room:room-rxjava3:$roomVersion"
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.room:room-ktx:$roomVersion"

    // navigation component
    api "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    api "androidx.navigation:navigation-fragment-ktx:$navigationVersion"

    // Rx
    api "io.reactivex.rxjava3:rxandroid:$rxAndroidVersion"
    api "io.reactivex.rxjava3:rxjava:$rxJavaVersion"

    // Network
    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    api "com.squareup.retrofit2:converter-gson:$retrofitVersion"
//    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:$retrofitAdapterVersion"
    // RxJava3 Adapter for retrofit

    // reactive network check
    implementation "com.github.pwittchen:reactivenetwork-rx2:$reactiveNetworkVersion"

    //imageLoader
    api "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // countly log service
    implementation "ly.count.android:sdk:$countlyVersion"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/module-release.aar")

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                // for dependencies and exclusions
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dp.group)
                    dependencyNode.appendNode('artifactId', dp.name)
                    dependencyNode.appendNode('version', dp.version)

                    // for exclusions
                    if (dp.excludeRules.size() > 0) {
                        def exclusions = dependencyNode.appendNode('exclusions')
                        dp.excludeRules.each { ExcludeRule ex ->
                            def exclusion = exclusions.appendNode('exclusion')
                            exclusion.appendNode('groupId', ex.group)
                            exclusion.appendNode('artifactId', ex.module)
                        }
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://maven2.mpars.ir/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = mpars_artifactory_username
            password = mpars_artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

task generateSources {
    doFirst {
        def script = "kotlinc.bat _generator_helper.kt -include-runtime -d _generator.jar".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()
    }
    doLast {
        def script = "java -jar _generator.jar".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()

        "rm _generator.jar".execute()
    }
}

task assembleModule {
    doFirst {
        def script = "gradlew.bat assemble".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()
    }
}

task publishModule {
    doFirst {
        def script = "gradlew.bat artifactoryPublish".execute()
        script.in.eachLine { line -> println line }
        script.err.eachLine { line -> println "ERROR: " + line }
        script.waitFor()
    }
}

assembleModule.dependsOn generateSources
publishModule.dependsOn assembleModule